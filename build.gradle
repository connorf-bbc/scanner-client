buildscript {
    ext {
        kotlin_version = '1.6.10'
        ktor_version = '1.6.8'
    }
}

plugins {
    id 'com.android.library'

    id 'org.jetbrains.kotlin.multiplatform' version "${kotlin_version}"
    id 'org.jetbrains.kotlin.plugin.serialization' version "${kotlin_version}"
    id 'org.jetbrains.kotlin.native.cocoapods' version "${kotlin_version}"

    id 'maven-publish'
}

group = 'uk.co.bbc'
version = '0.0.1-SNAPSHOT'

repositories {
    google()
    mavenCentral()
}

kotlin {
    jvm {
        compilations.all {
            kotlinOptions.jvmTarget = '1.8'
        }
        testRuns["test"].executionTask.configure {
            useJUnitPlatform()
        }
    }
    android {
        publishLibraryVariants("release")
    }
    ios()

    sourceSets {
        commonMain {
            dependencies {
                implementation "io.ktor:ktor-client-core:$ktor_version"

                implementation "io.ktor:ktor-client-serialization:$ktor_version"
            }
        }
        commonTest {
            dependencies {
                implementation kotlin('test')
            }
        }
        jvmMain {
            dependencies {
                implementation "io.ktor:ktor-client-cio:$ktor_version"
            }
        }
        jvmTest {

        }
        androidMain {
            dependencies {
                implementation "io.ktor:ktor-client-cio:$ktor_version"
            }
        }
        androidTest {
            dependencies {
                implementation 'junit:junit:4.13'
            }
        }
        
        iosMain {
            dependencies {
                implementation "io.ktor:ktor-client-ios:$ktor_version"
            }
        }
    }

    cocoapods {
        summary = "Scanner Client"
        homepage = "https://github.com/connorf-bbc/scanner-client"
        ios.deploymentTarget = "11.3"
        podfile = project.file("/Users/fordc02/dev/ios/smp-ios/Podfile")
    }
}

android {
    compileSdkVersion 29
    sourceSets.main.manifest.srcFile('src/androidMain/AndroidManifest.xml')
    defaultConfig {
        minSdkVersion 21
        targetSdkVersion 29
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}